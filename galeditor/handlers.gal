lowkey editor.shift_up{}
    fax shift_y = [GLOBAL_STATE "shift_y"]
    fax shift_y = shift_y - 1
    [GLOBAL_STATE "shift_y"] = shift_y

    foreal shift_y < 0
        [GLOBAL_STATE "shift_y"] = 0
    end
end

lowkey editor.shift_down{}
    fax shift_y = [GLOBAL_STATE "shift_y"]
    fax shift_y = shift_y + 1
    [GLOBAL_STATE "shift_y"] = shift_y

    fax lines = [GLOBAL_STATE "lines"]
    fax length = fire std.len_indecies(lines)
    fax term_height = [GLOBAL_STATE "term_height"]

    foreal shift_y + term_height > length
        [GLOBAL_STATE "shift_y"] = length - term_height
    end
end

lowkey editor.shift_left{}
    fax shift_x = [GLOBAL_STATE "shift_x"]
    fax shift_x = shift_x - 1
    [GLOBAL_STATE "shift_x"] = shift_x

    foreal shift_x < 0
        [GLOBAL_STATE "shift_x"] = 0
    end
end

lowkey editor.shift_right{}
    fax shift_x = [GLOBAL_STATE "shift_x"]
    fax shift_x = shift_x + 1
    [GLOBAL_STATE "shift_x"] = shift_x

    fax lines = [GLOBAL_STATE "lines"]
    fax editing_line = [GLOBAL_STATE "editing_line"]
    fax line = [lines editing_line]
    fax length = fire std.len(line)
    fax term_width = [GLOBAL_STATE "term_width"]

    foreal shift_x > length - term_width
        [GLOBAL_STATE "shift_x"] = length - term_width
    end

    foreal [GLOBAL_STATE "shift_x"] < 0
        [GLOBAL_STATE "shift_x"] = 0
    end
end

lowkey editor.on_press_arrow{press}
    foreal [GLOBAL_STATE "is_escape"] == nay
        rizzult nay
    end

    foreal press == 65
        [GLOBAL_STATE "is_escape"] = nay

        fax y = [GLOBAL_STATE "cursor_y"] - 1
        [GLOBAL_STATE "cursor_y"] = y

        fax temp = [GLOBAL_STATE "editing_line"] - 1
        [GLOBAL_STATE "editing_line"] = temp

        foreal [GLOBAL_STATE "editing_line"] < 0
            [GLOBAL_STATE "editing_line"] = 0
        end

        foreal [GLOBAL_STATE "cursor_y"] < 1
            [GLOBAL_STATE "cursor_y"] = 1
            fire editor.shift_up()
        end

        rizzult yay
    end

    foreal press == 66
        [GLOBAL_STATE "is_escape"] = nay

        fax y = [GLOBAL_STATE "cursor_y"] + 1
        [GLOBAL_STATE "cursor_y"] = y

        fax temp = [GLOBAL_STATE "editing_line"] + 1
        [GLOBAL_STATE "editing_line"] = temp

        fax lines = [GLOBAL_STATE "lines"]
        fax line = [lines temp]
        foreal line == nuthin
            [lines temp] = ""
        end
        [GLOBAL_STATE "lines"] = lines

        foreal [GLOBAL_STATE "cursor_y"] > [GLOBAL_STATE "term_height"]
            [GLOBAL_STATE "cursor_y"] = [GLOBAL_STATE "term_height"]
            fire editor.shift_down()
        end

        rizzult yay
    end

    foreal press == 67
        [GLOBAL_STATE "is_escape"] = nay

        fax x = [GLOBAL_STATE "cursor_x"] + 1

        [GLOBAL_STATE "cursor_x"] = x

        foreal [GLOBAL_STATE "cursor_x"] > [GLOBAL_STATE "term_width"]
            [GLOBAL_STATE "cursor_x"] = [GLOBAL_STATE "term_width"]
            fire editor.shift_right()
        end

        rizzult yay
    end

    foreal press == 68
        [GLOBAL_STATE "is_escape"] = nay
        fax x = [GLOBAL_STATE "cursor_x"] - 1

        [GLOBAL_STATE "cursor_x"] = x

        foreal [GLOBAL_STATE "cursor_x"] < 1
            [GLOBAL_STATE "cursor_x"] = 1
            fire editor.shift_left()
        end

        rizzult yay
    end

    rizzult nay
end

lowkey editor.on_press_exit{press}
    foreal press == 3
        fire clear_screen()
        fire set_cursor(0, 0)
        fax _ = fire saving_loop()
        fire clear_screen()
        fire set_cursor(0, 0)
        fire std.exit()
    end
end

lowkey editor.on_press_char{press}
    fax char = fire std.char(press)
    ` escape
    foreal [GLOBAL_STATE "is_escape"] == yay
        rizzult 0
    end
    foreal press == 27
        [GLOBAL_STATE "is_escape"] = yay
        rizzult 0
    end

    fax writable = fire std.writable(press)
    foreal writable === nay
        rizzult 0
    end

    ` backspace
    foreal press == 127
        fax lines = [GLOBAL_STATE "lines"]
        fax editing_line = [GLOBAL_STATE "editing_line"]
        fax line = [lines editing_line]
        foreal line === nuthin
            line = ""
        end
        fax length = fire std.len(line) - 1
        foreal length < 0
            length = 0
        end
        fax line = fire std.slice(line, 0, length)
        [lines editing_line] = line
        [GLOBAL_STATE "lines"] = lines
        rizzult 0
    end

    fax lines = [GLOBAL_STATE "lines"]
    fax editing_line = [GLOBAL_STATE "editing_line"]
    fax line = [lines editing_line]
    fax line = line + char
    [lines editing_line] = line
    [GLOBAL_STATE "lines"] = lines
    rizzult 0
end